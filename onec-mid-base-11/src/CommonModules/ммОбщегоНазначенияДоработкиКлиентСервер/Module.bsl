#Область ПрограммныйИнтерфейс

// Процедура программно добавляет на форму реквизит Контактное лицо
//
// Параметры:
//  Форма - Форма - Форма документа.
//  
//  Элемент - ЭлементыФормы - Элемент формы, относительно которого нужно вставить требуемый элемент
//
Процедура ДобавитьКонтактноеЛицоНаФорму(Форма, Элемент) Экспорт
	
	Если Форма.ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		
		ПолеВвода = Форма.Элементы.Вставить("ЭлементКонтактноеЛицо", Тип("ПолеФормы"), , Элемент);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.мм_КонтактноеЛицо";
		
	ИначеЕсли Форма.ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда 
		
		ПолеВвода = Форма.Элементы.Добавить("ЭлементКонтактноеЛицо", Тип("ПолеФормы"), Элемент);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.мм_КонтактноеЛицо";		
	КонецЕсли;
	
КонецПроцедуры

// Процедура программно добавляет на форму документа ЗаказПокупателя реквизит Согласованная скидка и команду
// 
// Параметры:
//  Форма - Форма - Форма документа.
//  
Процедура ДобавитьСкидкуНаФорму(Форма) Экспорт
	
	ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидка.ОтображатьЗаголовок = Ложь;
	
	ПолеСкидка = Форма.Элементы.Добавить("ЭлементСкидка", Тип("ПолеФормы"), ГруппаСкидка);
	ПолеСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСкидка.ПутьКДанным = "Объект.мм_СогласованнаяСкидка";
	ПолеСкидка.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
	
	КомандаСкидка = Форма.Команды.Добавить("ПересчитатьСумму");
	КомандаСкидка.Заголовок = "Пересчитать сумму";
	КомандаСкидка.Картинка = БиблиотекаКартинок.Обновить;
	КомандаСкидка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КомандаСкидка.Действие = "КомандаПересчитатьСумму";
	
	КнопкаПересчитатьСумму = Форма.Элементы.Добавить("КнопкаПересчитатьСумму", Тип("КнопкаФормы"), ГруппаСкидка);
	КнопкаПересчитатьСумму.ИмяКоманды = "ПересчитатьСумму";
	КнопкаПересчитатьСумму.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
КонецПроцедуры

#КонецОбласти